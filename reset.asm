.SEGMENT "CODE"
RESET:
    SEI        ; IGNORE IRQS
    CLD        ; DISABLE DECIMAL MODE
    LDX #$40
    STX $4017  ; DISABLE APU FRAME IRQ
    LDX #$FF
    TXS        ; SET UP STACK
    INX        ; NOW X = 0
    STX PPUCTRL  ; DISABLE NMI
    STX PPUMASK  ; DISABLE RENDERING
    STX $4010  ; DISABLE DMC IRQS

    ; OPTIONAL (OMITTED):
    ; SET UP MAPPER AND JMP TO FURTHER INIT CODE HERE.

    ; THE VBLANK FLAG IS IN AN UNKNOWN STATE AFTER RESET,
    ; SO IT IS CLEARED HERE TO MAKE SURE THAT @VBLANKWAIT1
    ; DOES NOT EXIT IMMEDIATELY.
    BIT PPUSTATUS

    ; FIRST OF TWO WAITS FOR VERTICAL BLANK TO MAKE SURE THAT THE
    ; PPU HAS STABILIZED
@VBLANKWAIT1:  
    BIT PPUSTATUS
    BPL @VBLANKWAIT1

    ; WE NOW HAVE ABOUT 30,000 CYCLES TO BURN BEFORE THE PPU STABILIZES.
    ; ONE THING WE CAN DO WITH THIS TIME IS PUT RAM IN A KNOWN STATE.
    ; HERE WE FILL IT WITH $00, WHICH MATCHES WHAT (SAY) A C COMPILER
    ; EXPECTS FOR BSS.  CONVENIENTLY, X IS STILL 0.
    TXA
@CLRMEM:
    STA $0000,X
    STA $0100,X
    STA $0200,X
    STA $0300,X
    STA $0400,X
    STA $0500,X
    STA $0600,X
    STA $0700,X
    INX
    BNE @CLRMEM

    ; OTHER THINGS YOU CAN DO BETWEEN VBLANK WAITS ARE SET UP AUDIO
    ; OR SET UP OTHER MAPPER REGISTERS.
   
@VBLANKWAIT2:
    BIT PPUSTATUS
    BPL @VBLANKWAIT2

    JMP MAIN