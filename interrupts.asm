.SEGMENT "CODE"
NMI:
    PHA
    TXA
    PHA
    TYA
    PHA   
OAM_CHK:
     LDA OAMFLAG
     BMI UPDATE_OAM
PPUCMD_CHK:
    LDA NumCommands
    BNE EXECUTE_PPU_CMD
PPUCTRL_CHK:
    LDA PPUCTRLFLAG
    BMI UPDATE_PPUCTRL
PPUMASK_CHK:
    LDA PPUMASKFLAG
    BMI UPDATE_PPUMASK
PPUSCROLL_CHK:
    LDA SCROLLFLAG
    BMI UPDATE_PPUSCROLL
    JMP EXIT_NMI
UPDATE_OAM:
    LDA #$02        ; TRANSFER SPRITE DATA FROM $XX00
    STA OAMDMA      ; TO PPU OAM  
    ASL OAMFLAG     ; CLEAR FLAG
    JMP PPUCMD_CHK
EXECUTE_PPU_CMD:
    JSR RD_BUF
    TAY             ; NUMBER OF BYTES TO WRITE TO VRAM
    BIT PPUSTATUS   ; RESET ADDRESS
    JSR RD_BUF      ; READ VRAM MSB
    STA PPUADDR     ; SET  VRAM MSB 
    JSR RD_BUF      ; READ VRAM LSB
    STA PPUADDR     ; SET VRAM LSB
PPUDATA_LOOP:
    JSR RD_BUF
    STA PPUDATA
    DEY
    BNE PPUDATA_LOOP
    DEC NumCommands
    BNE EXECUTE_PPU_CMD
    ;ASL PPUCMDFLAG     ; CLEAR FLAG
    JMP PPUCTRL_CHK
UPDATE_PPUCTRL:
    LDA PPUCTRLBUF
    STA PPUCTRL
    ASL PPUCTRLFLAG     ; CLEAR FLAG
    JMP PPUMASK_CHK
UPDATE_PPUMASK:
    LDA PPUMASKBUF
    STA PPUMASK
    ASL PPUMASKFLAG     ; CLEAR FLAG
    JMP PPUSCROLL_CHK
UPDATE_PPUSCROLL:
    BIT PPUSTATUS   ; RESET ADDRESS
    LDA ViewPort + ViewPort::Begin
    STA PPUSCROLL
    LDA ViewPort + ViewPort::Begin + 2
    STA PPUSCROLL
    LDA PPUCTRLBUF
    STA PPUCTRL
    ASL SCROLLFLAG ; CLEAR FLAG
EXIT_NMI:
    ASL NMI_DONE    ; CLEAR FLAG
    PLA
    TAY
    PLA
    TAX
    PLA
    RTI

;------------------------------------------------------------------------
IRQ:
RTI