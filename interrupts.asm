.SEGMENT "CODE"
NMI:
    PHA
    TXA
    PHA
    TYA
    PHA   
OAM_CHK:
     LDA OAMFLAG
     BPL PPUCMD_CHK
UPDATE_OAM:
    LDA #$02        ; TRANSFER SPRITE DATA FROM $XX00
    STA OAMDMA      ; TO PPU OAM  
PPUCMD_CHK:
    LDA NumCommands
    BEQ PPUREG_CHK
EXECUTE_PPU_CMD:
    JSR RD_BUF
    TAY             ; NUMBER OF BYTES TO WRITE TO VRAM
    BIT PPUSTATUS   ; RESET ADDRESS
    JSR RD_BUF      ; READ VRAM MSB
    STA PPUADDR     ; SET  VRAM MSB 
    JSR RD_BUF      ; READ VRAM LSB
    STA PPUADDR     ; SET VRAM LSB
    JSR RD_BUF      ; DRAW FLAGS
    STA PPUCTRL
PPUDATA_LOOP:
    JSR RD_BUF
    STA PPUDATA
    DEY
    BNE PPUDATA_LOOP
    DEC NumCommands
PPUREG_CHK:
    LDA PPUREGFLAG
    BPL SCROLL_SPLIT
UPDATE_PPUREG:
    LDA PPUMASKBUF
    STA PPUMASK
SCROLL_SPLIT:
    LDA PPUCTRLBUF
    AND #$FE
    STA PPUCTRL
    BIT PPUSTATUS   ; RESET ADDRESS
    LDA #$00
    STA PPUSCROLL
    STA PPUSCROLL
:   BIT PPUSTATUS
    BVS :-
:   BIT PPUSTATUS
    BVC :-
    BIT PPUSTATUS   ; RESET ADDRESS
    LDA PPUCTRLBUF
    STA PPUCTRL
    LDA ViewPort + ViewPort::Begin
    LDX #$0A
:   DEX
    BNE :-
    STA PPUSCROLL
EXIT_NMI:
    ASL NMI_DONE    ; CLEAR FLAG    
    PLA
    TAY
    PLA
    TAX
    PLA
    RTI

;------------------------------------------------------------------------
IRQ:
RTI