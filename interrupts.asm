.SEGMENT "CODE"
NMI:
    PHA
    TXA
    PHA
    TYA
    PHA   
OAM_CHK:
     LDA OAMFLAG
     BPL PPUCMD_CHK
UPDATE_OAM:
    LDA #$02        ; TRANSFER SPRITE DATA FROM $XX00
    STA OAMDMA      ; TO PPU OAM  
PPUCMD_CHK:
    LDA NumCommands
    BEQ PPUREG_CHK
EXECUTE_PPU_CMD:
    JSR RD_BUF
    TAY             ; NUMBER OF BYTES TO WRITE TO VRAM
    BIT PPUSTATUS   ; RESET ADDRESS
    JSR RD_BUF      ; READ VRAM MSB
    STA PPUADDR     ; SET  VRAM MSB 
    JSR RD_BUF      ; READ VRAM LSB
    STA PPUADDR     ; SET VRAM LSB
PPUDATA_LOOP:
    JSR RD_BUF
    STA PPUDATA
    DEY
    BNE PPUDATA_LOOP
    DEC NumCommands
PPUREG_CHK:
    LDA PPUREGFLAG
    BPL EXIT_NMI
UPDATE_PPUREG:
    LDA PPUCTRLBUF
    STA PPUCTRL
    LDA PPUMASKBUF
    STA PPUMASK
    BIT PPUSTATUS   ; RESET ADDRESS
    LDA ViewPort + ViewPort::Begin
    STA PPUSCROLL
    LDA ViewPort + ViewPort::Begin + 2
    STA PPUSCROLL
EXIT_NMI:
    ASL NMI_DONE    ; CLEAR FLAG
    PLA
    TAY
    PLA
    TAX
    PLA
    RTI

;------------------------------------------------------------------------
IRQ:
RTI